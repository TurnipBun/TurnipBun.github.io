---
layout: post
title:  "使用CMake构建QT项目"
summary: 2020年底发布的Qt6有一大变化是Qt6本身使用CMake进行构建和打包.尽管qmake会继续得到支持,但是官方推荐使用CMake.让我们来看看如何使用CMake构建QT项目吧!
featured-img: qt
---

## 简介 ##
CMake支持查找Qt4和Qt5库.CMake的FindQt4查找模块可以查找Qt4库,而Qt5则使用"Config-file Packages"来查找Qt5库.
Qt4和Qt5可以在同一个CMake构建系统一起使用.
```
cmake_minimum_required(VERSION 3.8.0 FATAL_ERROR)

project(Qt4And5)

set(CMAKE_AUTOMOC ON)

find_package(Qt5 COMPONENTS Widgets DBus REQUIRED)
add_executable(publisher publisher.cpp)
target_link_libraries(publisher Qt5::Widgets Qt5::DBus)

find_package(Qt4 REQUIRED)
add_executable(subscriber subscriber.cpp)
target_link_libraries(subscriber Qt4::QtGui Qt4::QtDBus)
```
但是一个CMake目标是不能同时链接Qt4和Qt5库的(会报错或导致检测到传递目标依赖).

## Qt 构建工具 ##
Qt依赖了一些自带的代码生成工具,比如用来生成元对象(meta-object)代码的moc,用来布局和填充窗口部件(widget)的uic,和用来生成虚拟文件系统内容的rcc.在条件满足的情况下,这些工具都可以由cmake来触发.这种自动工具触发功能Qt4和Qt5都能用.

### AUTOMOC  ###
AUTOMOC目标属性控制cmake检查目标依赖的C++文件以确定是否运行moc工具,并创建在合适的时间运行工具的规则.
如果在头文件中找到AUTOMOC_MACRO_NAMES中的某个宏,moc就会在这个文件上运行,运行的结果会放到`moc_<basename>.cpp`文件中.如果在C++实现文件中找到宏,运行的结果或放到`<basename>.moc`文件中,这与Qt的惯例一致.用户必须在C++实现文点中包含生成的`<basename>.moc`文件.
生成的需要被包含的moc_*.cpp和*.moc文件位于`<AUTOGEN_BUILD_DIR>/include`目录下,这个目录会自动加到目标的INCLUDE_DIRECTORIES属性中.
不需要被包含的`moc_<basename>.cpp`文件会被放在自定义的目录下以防止名称冲突,并且在单独的`<AUTOGEN_BUILD_DIR>/mocs_compilation.cpp`文件中去包含它们,最终这个文件会编译到目标中去.
AUTOMOC目标属性可以在CMAKE_AUTOMOC变量中进行预设置(后续的cmake目标无需再做单独设置).AUTOMOC_MOC_OPTIONS目标属性用来设置传递给moc的命令行选项,CMAKE_AUTOMOC_MOC_OPTIONS可以为这个命令行选项做预设值.
AUTOMOC_MACRO_NAMES变量可以添加额外的搜索宏.
AUTOMOC_DEPEND_FILTERS变量可以从源代码提取额外的依赖文件名.
SKIP_AUTOMOC和SKIP_AUTOGEN可以用来排除C++源文件的AUTOMOC处理.

### AUTOUIC ###
AUTOUIC目标属性控制cmake检查目标依赖的C++文件以确定是否运行uic工具,并创建在合适的时间运行工具的规则.
如果在#include语句中找到了匹配模式`<path>ui_<basename>.h`的内容,并且存在`<basename>.ui`一个文件,那么uic工具会被执行并生成相关文件.`<basename>.ui`文件的搜索路径有下面几种.
1. `<source_dir>/<basename>.ui`
2. `<source_dir>/<path><basename>.ui`
3. `<AUTOUIC_SEARCH_PATHS>/<basename>.ui`
4. `<AUTOUIC_SEARCH_PATHS>/<path><basename>.ui`

其中`<source_dir>`是C++文件所在目录,AUTOUIC_SEARCH_PATHS是设置的额外搜索路径.
生成的ui_*.h文件位于`<AUTOGEN_BUILD_DIR>/include`目录下,这个路径会自动添加到目标的INCLUDE_DIRECTORIES中.
AUTOUIC目标属性可以通过CMAKE_AUTOUIC变量为后续的目标做预设值.目标属性AUTOUIC_OPTIONS用来设置传递给uic的命令行参数.CMAKE_AUTOUIC_OPTIONS变量同样可以对命令行参数做预设值.文件的AUTOUIC_OPTIONS属性可以对`<basename>.ui`文件设置单独的属性,这将覆盖目标的AUTOUIC_OPTIONS属性.
一个目标可以填入INTERFACE_AUTOUIC_OPTIONS目标属性,用来设置uic会用到的选项.它必须与从属目标的AUTOUIC_OPTIONS属性的内容一致.变量CMAKE_DEBUG_TARGET_PROPERTIES可以用来跟踪原始目标的INTERFACE_AUTOUIC_OPTIONS属性.这意味着一个提供了替代翻译系统的库(下面的例子)需要指定一些uic会用到的选项.
```
add_library(KI18n klocalizedstring.cpp)
target_link_libraries(KI18n Qt5::Core)

# KI18n 使用 tr2i18n() 函数代替 tr(). 函数在头文件klocalizedstring.h中声明
set(autouic_options
  -tr tr2i18n
  -include klocalizedstring.h
)

set_property(TARGET KI18n APPEND PROPERTY
  INTERFACE_AUTOUIC_OPTIONS ${autouic_options}
)
```
链接导入目标的情况下,当uic会被AUTOUIC触发时,它的参数选项可以从上游自动获取.
```
set(CMAKE_AUTOUIC ON)
# 使用libwidget.ui文件
add_library(LibWidget libwidget.cpp)
target_link_libraries(LibWidget
  KF5::KI18n
  Qt5::Widgets
)
```
SKIP_AUTOUIC和SKIP_AUTOGEN可以排除C++源文件的AUTOUIC处理.

## AUTORCC ##
AUTORCC目标属性控制cmake在检查以.qrc为后缀的文件以确定是否运行rrc工具,并创建在合适的时间运行工具的规则.
```
add_executable(myexe main.cpp resource_file.qrc)
```
AUTORCC目标属性可以通过变量CMAKE_AUTORCC为后续的目标做预设置.目标属性AUTORCC_OPTIONS用来设置传递给rcc的命令行参数.变量CMAKE_AUTORCC_OPTIONS用来为后续的目标做rcc命令行参数的预设置.源文件的AUTORCC_OPTIONS属性可以为`<name>.qrc`文件设置单独的选项,这将覆盖目标的AUTORCC_OPTIONS属性.
SKIP_AUTORCC和SKIP_AUTOGEN可以排除源文件的AUTORCC处理.

## 快速开始 ##
首先使用find_package来定位Qt的库文件和头文件.接着才能在target_link_libraries命令中使用这些库和头文件来构建库和应用程序.该命令可以自动添加合适的头文件包含路径,编译选项,位置无关代码选项,和链接qtmain.lib(Windows平台).

### 构建GUI可执行程序 ###
构建一个helloworld GUI可执行程序的CMakeLists.txt文件内容如下:
```
cmake_minimum_required(VERSION 3.1.0)

project(helloworld VERSION 1.0.0 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTORCC ON)
set(CMAKE_AUTOUIC ON)

if(CMAKE_VERSION VERSION_LESS "3.7.0")
    set(CMAKE_INCLUDE_CURRENT_DIR ON)
endif()

find_package(Qt5 COMPONENTS Widgets REQUIRED)

add_executable(helloworld
    mainwindow.ui
    mainwindow.cpp
    main.cpp
    resources.qrc
)

target_link_libraries(helloworld Qt5::Widgets)
```
find_package命令执行成功时,CMake会通过以下两种方式获取Qt安装位置.
1. CMAKE_PREFIX_PATH环境变量设置了Qt5安装位置(推荐).
2. 找到Qt5Config.cmake文件所在位置,在CMake缓存中设置Qt5_DIR.

### 导入库目标 ###
每一个被导入的Qt模块都是一个CMake库目标.目标的名称以Qt5::开始,接着是模块的名称.例如,Qt5::Core和Qt5::Gui.将库目标的名称传入target_link_libraries命令就可以使用这些库了.
注意,从Qt5.15开始,可以通过Qt::Core, Qt::Gui这种形式访问CMake库目标.这样CMake代码在Qt5和Qt6都能正常工作.
导入Qt库的目标会使用和Qt一样的配置.
 * 如果Qt配置了-debug开关,创建一个使用DEBUG配置的目标.
 * 如果Qt配置了-release开关,创建一个使用RELEASE配置的目标.
 * 如果Qt配置了-debug-and-release开关,同时创建RELEASE和DEBUG配置的目标.
如果你的工程有自定义的CMake构建配置,需要将自定义配置映射到debug或release的Qt配置.
```
find_package(Qt5 COMPONENTS Core REQUIRED)
set(CMAKE_CXX_FLAGS_COVERAGE "${CMAKE_CXX_FLAGS_RELEASE} -fprofile-arcs -ftest-coverage")
# 设置一个映射,在COVERAGE CMake配置中使用Qt的Relase配置
set_target_properties(Qt5::Core PROPERTIES MAP_IMPORTED_CONFIG_COVERAGE "RELEASE")
```
